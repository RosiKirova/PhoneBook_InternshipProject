USE CSoft_Intern_Phonebook_Task1
GO


DROP TABLE IF EXISTS PHONE_NUMBERS
GO

DROP TABLE IF EXISTS PERSONS
GO

DROP TABLE IF EXISTS CITIES
GO

DROP TABLE IF EXISTS PHONE_TYPES
GO

DROP PROCEDURE IF EXISTS SP_ADD_TABLE_DESCRIPTION
GO

DROP PROCEDURE IF EXISTS SP_ADD_COLUMN_DESCRIPTION
GO


CREATE PROCEDURE SP_ADD_TABLE_DESCRIPTION @TABLE_NAME NCHAR(32), @TABLE_DESCRIPTION NCHAR(128)
AS
IF NOT EXISTS ( SELECT NULL
				FROM SYS.EXTENDED_PROPERTIES
				WHERE major_id = OBJECT_ID(@TABLE_NAME) AND name = N'SP_ADD_TABLE_DESCRIPTION' AND minor_id = 0)
BEGIN
	EXEC sp_addextendedproperty
	@name = N'SP_ADD_TABLE_DESCRIPTION',
	@value = @TABLE_DESCRIPTION,
	@level0type = N'Schema', @level0name = dbo,
	@level1type = N'Table', @level1name = @TABLE_NAME
END
GO

CREATE PROCEDURE SP_ADD_COLUMN_DESCRIPTION @TABLE_NAME NCHAR(32), @COLUMN_NAME NCHAR(32), @TABLE_DESCRIPTION NCHAR(128)
AS
IF NOT EXISTS ( SELECT NULL
				FROM SYS.EXTENDED_PROPERTIES
				WHERE major_id = OBJECT_ID(@TABLE_NAME) AND name = N'SP_ADD_COLUMN_DESCRIPTION'
				AND minor_id = (SELECT column_id
								FROM SYS.COLUMNS
								WHERE name = @COLUMN_NAME AND object_id = OBJECT_ID(@TABLE_NAME)))
BEGIN
	EXEC sp_addextendedproperty
	@name = N'SP_ADD_COLUMN_DESCRIPTION',
	@value = @TABLE_DESCRIPTION,
	@level0type = N'Schema', @level0name = dbo,
	@level1type = N'Table', @level1name = @TABLE_NAME,
	@level2type = N'Column', @level2name = @COLUMN_NAME
END
GO


CREATE TABLE CITIES
(
	ID INT IDENTITY(1,1),
	UPDATE_COUNTER INT NOT NULL,
	[NAME] NCHAR(128) NOT NULL,
	REGION NCHAR(128) NOT NULL
	CONSTRAINT PK_CITIES_ID PRIMARY KEY (ID)
)
GO

CREATE UNIQUE INDEX UX_CITIES_NAME_REGION
ON CITIES([NAME], REGION)
GO


EXEC SP_ADD_TABLE_DESCRIPTION N'CITIES', N'Table with the names and regions of cities'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'CITIES', N'ID', N'Unique identifier'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'CITIES', N'UPDATE_COUNTER', N'Update times'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'CITIES', N'NAME', N'The name of the city'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'CITIES', N'REGION', N'The region of the city'
GO


CREATE TABLE PHONE_TYPES
(
	ID INT IDENTITY(1,1),
	UPDATE_COUNTER INT NOT NULL,
	[TYPE] NCHAR(16) NOT NULL,
	CONSTRAINT PK_PHONE_TYPES_ID PRIMARY KEY (ID)
)
GO

CREATE UNIQUE INDEX UX_PHONE_TYPES_TYPE
ON PHONE_TYPES([TYPE])
GO


EXEC SP_ADD_TABLE_DESCRIPTION N'PHONE_TYPES', N'Table with phone numbers types - mobile, home and professional'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PHONE_TYPES', N'ID', N'Unique identifier'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PHONE_TYPES', N'UPDATE_COUNTER', N'Update times'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PHONE_TYPES', N'TYPE', N'The type of the phone number'
GO


CREATE TABLE PERSONS
(
	ID INT IDENTITY(1,1),
	UPDATE_COUNTER INT NOT NULL,
	FIRST_NAME NCHAR(64) NOT NULL,
	MIDDLE_NAME NCHAR(64) NOT NULL,
	LAST_NAME NCHAR(64) NOT NULL,
	UCN NCHAR(16) NOT NULL,
	CITY_ID INT NOT NULL,
	[ADDRESS] NCHAR(256) NOT NULL,
	CONSTRAINT PK_PERSONS_ID PRIMARY KEY (ID),
	CONSTRAINT FK_PERSONS_CITY_ID FOREIGN KEY (CITY_ID) REFERENCES CITIES(ID)
) 
GO

CREATE UNIQUE INDEX UX_PERSONS_UCN
ON PERSONS(UCN)
GO

CREATE INDEX IX_PERSONS_CITY_ID
ON PERSONS(CITY_ID)
GO


EXEC SP_ADD_TABLE_DESCRIPTION N'PERSONS', N'Table with persons and their details'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PERSONS', N'ID', N'Unique identifier'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PERSONS', N'UPDATE_COUNTER', N'Update times'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PERSONS', N'FIRST_NAME', N'First name of the person'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PERSONS', N'MIDDLE_NAME', N'Middle name of the person'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PERSONS', N'LAST_NAME', N'Last name of the person'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PERSONS', N'UCN', N'UCN of the person'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PERSONS', N'CITY_ID', N'The unique identifier of the city of the person'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PERSONS', N'ADDRESS', N'Address of the person'
GO


CREATE TABLE PHONE_NUMBERS(
	ID INT IDENTITY(1,1),
	UPDATE_COUNTER INT NOT NULL,
	PERSON_ID INT NOT NULL,
	PHONE_TYPE_ID INT NOT NULL,
	PHONE_NUMBER NCHAR(32) NOT NULL,
	CONSTRAINT PK_PHONE_NUMBERS_ID PRIMARY KEY (ID),
	CONSTRAINT FK_PHONE_NUMBERS_PERSON_ID FOREIGN KEY (PERSON_ID) REFERENCES PERSONS(ID),
	CONSTRAINT FK_PHONE_NUMBERS_PHONE_TYPE_ID FOREIGN KEY (PHONE_TYPE_ID) REFERENCES PHONE_TYPES(ID)
)
GO

CREATE INDEX IX_PHONE_NUMBERS_PERSON_ID
ON PHONE_NUMBERS (PERSON_ID)
GO

CREATE INDEX IX_PHONE_NUMBERS_PHONE_TYPE_ID
ON PHONE_NUMBERS (PHONE_TYPE_ID)
GO


EXEC SP_ADD_TABLE_DESCRIPTION N'PHONE_NUMBERS', N'Table with phone numbers'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PHONE_NUMBERS', N'ID', N'Unique identifier'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PHONE_NUMBERS', N'UPDATE_COUNTER', N'Update times'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PHONE_NUMBERS', N'PERSON_ID', N'The unique identifier of the person who owns this phone number'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PHONE_NUMBERS', N'PHONE_TYPE_ID', N'The unique identifier of the type of the phone number'
GO

EXEC SP_ADD_COLUMN_DESCRIPTION N'PHONE_NUMBERS', N'PHONE_NUMBER', N'Phone number'
GO



